buildscript {
    repositories {
        jcenter()
        maven { url = 'http://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = '1.12.2-1.0.0'
group = 'tehseph.netherfoundation'
archivesBaseName = 'NetherFoundation'

compileJava { sourceCompatibility = targetCompatibility = '1.8' }

minecraft {
    version = '1.12.2-14.23.3.2673'
    mappings = 'snapshot_20180425' // 'snapshot_20171003'
    runDir = 'run'
    makeObfSourceJar = false
}

repositories {
    maven { url = 'http://maven.covers1624.net' } // Team CoFH
    maven { url = 'http://chickenbones.net/maven/' } // Chickenbones
    maven { url = 'http://dvs1.progwml6.com/files/maven' } // JEI
    maven { url = 'http://tehnut.info/maven' } // HWYLA/WAILA
}

dependencies {
    compile 'cofh:CoFHCore:1.12.2-4.4.0.+:deobf'
    compile 'cofh:CoFHWorld:1.12.2-1.1.1.+:deobf'
    compile 'cofh:ThermalFoundation:1.12.2-2.4.0.+:deobf'
    compile 'cofh:ThermalExpansion:1.12.2-5.4.0.+:deobf'
    compile 'mcp.mobius.waila:Hwyla:1.8.25-B40_1.12'
    compile 'mezz.jei:jei_1.12.2:4.9.1.+'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar { classifier = 'universal' }

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}
